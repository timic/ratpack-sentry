buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id 'groovy'
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'jacoco'
  id "com.github.hierynomus.license" version "0.15.0"
  id 'com.palantir.git-version' version '0.12.3'
  id 'com.github.ben-manes.versions' version '0.33.0'
}

group 'com.github.timic'

def ver = versionDetails()
if (ver.isCleanTag) {
  version gitVersion()
} else {
  version "${ver.lastTag.replace("-SNAPSHOT", "")}-SNAPSHOT"
}

repositories {
  mavenCentral()
}

dependencies {
  compileOnly "io.ratpack:ratpack-core:$ratpackVersion"
  api "io.sentry:sentry:$sentryVersion"

  testCompile "io.ratpack:ratpack-groovy-test:$ratpackVersion"
  testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
  testCompile "io.sentry:sentry-logback:$sentryVersion"
  testCompile 'org.skyscreamer:jsonassert:1.5.0'

  // for jdk 9, 10 compatibility
  testCompile "javax.activation:activation:1.1.1"
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    ratpackSentry(MavenPublication) {
      artifactId = 'ratpack-sentry'
      from components.java
      pom {
        name = "ratpack-sentry"
        description = "Library to integrate Sentry an error tracking solution and Ratpack web-framework."
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'timic'
            name = 'Timur Salyakhutdinov'
            email = 'timic@eml.cc'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/timic/ratpack-sentry.git'
          developerConnection = 'scm:git:ssh://git@github.com:timic/ratpack-sentry.git'
          url = 'https://github.com/timic/ratpack-sentry'
        }
      }
    }
  }
  repositories {
    maven {
      if (version.endsWith('SNAPSHOT')) {
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
      }  else {
        url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
      }
      credentials {
        username findProperty('sonatypeUser') ?: 'timic'
        password findProperty('sonatypePassword')
      }
    }
  }
}

signing {
  sign publishing.publications.ratpackSentry
}

jar.dependsOn(check)

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled false
  }
}

check.dependsOn jacocoTestReport