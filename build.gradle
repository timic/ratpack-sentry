buildscript {
  repositories {
    jcenter()
  }
}

plugins {
  id 'groovy'
  id 'java-library'
  id 'maven-publish'
  id 'jacoco'
  id 'com.jfrog.bintray' version '1.8.5'
  id "com.github.hierynomus.license" version "0.15.0"
  id 'com.palantir.git-version' version '0.12.3'
  id 'com.github.ben-manes.versions' version '0.33.0'
}

group 'com.github.timic'
version gitVersion()

repositories {
  jcenter()
}

dependencies {
  compileOnly "io.ratpack:ratpack-core:$ratpackVersion"
  api "io.sentry:sentry:$sentryVersion"

  testCompile "io.ratpack:ratpack-groovy-test:$ratpackVersion"
  testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
  testCompile "io.sentry:sentry-logback:$sentryVersion"
  testCompile 'org.skyscreamer:jsonassert:1.5.0'

  // for jdk 9, 10 compatibility
  testCompile "javax.activation:activation:1.1.1"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

publishing {
  publications {
    ratpackSentry(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
  }
}

bintray {
  user = System.getenv("BINTRAY_USER")
  key = System.getenv("BINTRAY_KEY")
  publications = ["ratpackSentry"]
  pkg {
    repo = "generic"
    name = "ratpack-sentry"
    userOrg = user
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/timic/ratpack-sentry.git'
    labels = ['ratpack', 'sentry']
  }
}

jar.dependsOn(check)

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled false
  }
}

check.dependsOn jacocoTestReport